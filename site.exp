# vim: set filetype=tcl :

# config file for DejaGnu (runtest)

set srcdir [file normalize ./testsuite]
set outdir [file normalize ./testsuite/log]

# definitions of common procedures

if { [catch {package require fileutil}] } {
    namespace eval fileutil { proc tempdir {} { return $outdir } }
}

proc mktempdir {prefix} {
    set chars abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789
    set numchars [string length $chars]
    set numtrial 10
    set namelen 10
    for {set i 0} {$i < $numtrial} {incr i} {
        set path [::fileutil::tempdir]/$prefix
        for {set j 0} {$j < $namelen} {incr j} {
            append path [string index $chars [expr {int(rand() * $numchars)}]]
        }
        if {![file exists $path]} {
            # note that we have race condition here
            file mkdir $path
            file attributes $path -permissions 0700
            return $path
        }
    }
    error "failed to find an unused temporary directory name"
}

proc exitstatus args {
    foreach {pid spawnid oserror status} [wait] {}
    if {[llength $args] == 0} { 
        return $status
    }
    if {[lindex $args 0] == "should" && [lindex $args 1] == "be"} {
        set expected [lindex $args 2]
        if {$status == $expected} {
            pass "exit status = $status"
        } else {
            if {[lindex $args 3] == "or"} {
                set msg [lindex $args 4]
            } else {
                set msg "exit status = $status, while $expected is expected"
            }
            fail $msg
        }
    } else {
        error "exitstatus: wrong argument"
    }
}

proc withdir {dir body} {
    set originaldir [pwd]
    cd $dir
    uplevel 1 $body
    cd $originaldir
}
